# Copyright (C) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../accessibility_manager_service.gni")

asacfwk_path = "../../../frameworks/asacfwk"

config("accessibilityclient_private_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "${asacfwk_path}/include",
    "${asacfwk_path}/src/rules",
    "../../../common/log/include",
    "../../../common/interface/include",
  ]

  defines = [
    "AAMS_LOG_TAG = \"accessibility_asacfwk\"",
    "AAMS_LOG_DOMAIN = 0xD001D02",
  ]

  defines += accessibility_default_defines

  if (build_variant == "user") {
    defines += [ "RELEASE_VERSION" ]
  }
}

config("accessibilityclient_public_config") {
  include_dirs = [ "include" ]
}

asacfwk_src = [
  "${asacfwk_path}/src/accessibility_system_ability_client_impl.cpp",
  "${asacfwk_path}/src/accessibility_element_operator_impl.cpp",
  "${asacfwk_path}/src/rules/rules_checker.cpp",
  "${asacfwk_path}/src/rules/condition_item.cpp",
  "${asacfwk_path}/src/rules/condition.cpp",
  "${asacfwk_path}/src/rules/custom_props.cpp",
  "${asacfwk_path}/src/rules/prop_value.cpp",
  "${asacfwk_path}/src/rules/readable_rule.cpp",
  "${asacfwk_path}/src/rules/rules_defines.cpp",
  "../../../common/interface/src/api_event_reporter.cpp",
]

ohos_shared_library("accessibilityclient") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = false
    cfi_cross_dso = false
    debug = false
  }

  sources = asacfwk_src
  configs = [
    ":accessibilityclient_private_config",
    "../../../resources/config/build:coverage_flags",
  ]

  public_configs = [ ":accessibilityclient_public_config" ]

  deps = [
    "../../../common/interface:accessibility_interface",
    "../common:accessibility_common",
  ]

  external_deps = [
    "c_utils:utils",
    "e2fsprogs:libext2_uuid",
    "ffrt:libffrt",
    "hilog:libhilog",
    "init:libbeget_proxy",
    "init:libbegetutil",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
    "json:nlohmann_json_static",
    "common_event_service:cesfwk_innerkits",
  ]

  if (accessibility_feature_hiviewdfx_hitrace) {
    external_deps += [ "hitrace:hitrace_meter" ]
  }

  if (product_name != "qemu-arm-linux-min") {
    external_deps += [ "hiappevent:hiappevent_innerapi" ]
  }

  innerapi_tags = [ "platformsdk" ]
  install_enable = true

  subsystem_name = "barrierfree"
  part_name = "accessibility"
}
